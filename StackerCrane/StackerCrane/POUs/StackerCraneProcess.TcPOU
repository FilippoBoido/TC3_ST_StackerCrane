<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.10">
  <POU Name="StackerCraneProcess" Id="{b4bd5f7d-e366-4bba-8281-bae6ed04b2d2}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK StackerCraneProcess
VAR_IN_OUT
	bStart : BOOL;
	bHalt : BOOL;
	bResume : BOOL;
	bStop : BOOL;
	fioOutputs : OUTPUTS;
END_VAR
VAR_INPUT
	fioInputs : INPUTS;
	iAlgo: INT;
	iMode: INT;
	iRackSlot : INT;
END_VAR
VAR_OUTPUT
	sCurrentOperation : STRING;
	iTotalPalletsProcessed : INT;
	iPalletsInRack : INT;
	aSlots : ARRAY[1..54] OF BOOL;

END_VAR
VAR
	iStep : INT;
	startTrig : R_TRIG;
	stopTrig : R_TRIG;
	resumeTrig : R_TRIG;
	haltTrig : R_TRIG;
	iHaltStep : INT;
	mAlgo : INT;
	mMode : INT;
	i : INT;
	iChosenSlot : INT;
	fill : Fill;
	empty : Empty;
	home : Home;
	iCounter :INT;
	exitTimer : TON;
	aFillVerticalPositions : ARRAY[1..54] OF INT := [
		1, 10, 19, 28, 37, 46,
		2, 11, 20, 29, 38, 47,
		3, 12, 21, 30, 39, 48,
		4, 13, 22, 31, 40, 49,
		5, 14, 23, 32, 41, 50,
		6, 15, 24, 33, 42, 51,
		7, 16, 25, 34, 43, 52,
		8, 17, 26, 35, 44, 53,
		9, 18, 27, 36, 45, 54
	];
	
	aEmptyVerticalPositions : ARRAY[1..54] OF INT := [
		54, 45, 36, 27, 18, 9,
		53, 44, 35, 26, 17, 8,
		52, 43, 34, 25, 16, 7,
		51, 42, 33, 24, 15, 6,
		50, 41, 32, 23, 14, 5,
		49, 40, 31, 22, 13, 4,
		48, 39, 30, 21, 12, 3,
		47, 38, 29, 20, 11, 2,
		46, 37, 28, 19, 10, 1
	];
END_VAR

VAR CONSTANT
	AVAILABLE_SLOTS: INT := 54;
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[startTrig(CLK:=bStart);
stopTrig(CLK:=bStop);
resumeTrig(CLK:=bResume);
haltTrig(CLK:=bHalt);
exitTimer(PT:=T#5S);

fill(
	fioOutputs:=fioOutputs,
	iPalletsInRack:=iPalletsInRack,
	sCurrentOperation:=sCurrentOperation,
	aSlots:=aSlots,
	fioInputs:=fioInputs);
	
empty(
	fioOutputs:=fioOutputs,
	iPalletsInRack:=iPalletsInRack,
	sCurrentOperation:=sCurrentOperation,
	aSlots:=aSlots,
	fioInputs:=fioInputs);	

home(
	fioOutputs:=fioOutputs,
	iPalletsInRack:=iPalletsInRack,
	sCurrentOperation:=sCurrentOperation,
	aSlots:=aSlots,
	fioInputs:=fioInputs);	
	
IF startTrig.Q 
	AND iStep = 0
	THEN
	resetInputs();
	mAlgo := iAlgo;
	mMode := iMode;
	iStep := 10;
END_IF

IF startTrig.Q 
	AND iStep = 2000
	THEN
	resetInputs();
	//Home
	iStep := 3000;
END_IF

IF resumeTrig.Q 
	AND iStep  = 1000
	THEN
	resetInputs();
	iStep := iHaltStep;	
END_IF

IF haltTrig.Q
	THEN
	resetInputs();
	iHaltStep := iStep;
	iStep := 1000;	
END_IF

IF stopTrig.Q
	THEN
	resetInputs();
	exitTimer.IN := FALSE;
	iStep :=  2000;
END_IF

CASE iStep OF
	0:
	 	sCurrentOperation := 'Idle';
		resetInputs();

	10: 
		IF mMode = 1 // Fill
			AND iPalletsInRack < 54
			AND NOT (mAlgo = 3 AND aSlots[iRackSlot]) 
			THEN
			iTotalPalletsProcessed := iTotalPalletsProcessed + 1;
			// Emit Pallet
			fioOutputs.bEmitPallet := TRUE;
			// Start Entry Conveyors
			fioOutputs.bEntryRollerConveyor := TRUE;
			fioOutputs.bEntryLoadingConveyor := TRUE;
			sCurrentOperation := 'Entry Conveyor started';
			iStep := 20;
		ELSIF mMode = 2
			AND iPalletsInRack > 0 // empty
			AND NOT (mAlgo = 3 AND NOT aSlots[iRackSlot]) 
			THEN
			iStep := 60;
			
		ELSE // Not mplemented yet
			iStep := 0;
		END_IF
		
	// ------------------------------------------------------------------------ Fill operation --------------------------------------------------------
	20: // Check Pallet in Loading Conveyor
		IF fioInputs.bEntrySensor
			THEN
			// Stop emitting and stop conveyors
			fioOutputs.bEntryRollerConveyor := FALSE;
			fioOutputs.bEntryLoadingConveyor := FALSE;
			fioOutputs.bEmitPallet := FALSE;	
			iStep := 30;	
		END_IF
		
	30: // Check if space available in rack
		IF iPalletsInRack < AVAILABLE_SLOTS
			THEN
			iStep := 40;
		ELSE
			// Halt operations
			iStep := 1000;
		END_IF
		
	40: // Check algo for fill operation
	
		IF mAlgo = 1 // Horizontal
			THEN
			sCurrentOperation := 'Filling horizontally';
			FOR i := 1 TO 54 DO
				IF aSlots[i] = FALSE
					THEN
					iChosenSlot := i;
					EXIT;
				END_IF
			END_FOR
			iStep := 50;
		ELSIF mAlgo = 2 // Vertical
			THEN
			sCurrentOperation := 'Filling vertically';
			FOR i := 1 TO 54 DO
				IF aSlots[aFillVerticalPositions[i]] = FALSE
					THEN
					iChosenSlot := aFillVerticalPositions[i];
					EXIT;
				END_IF
			END_FOR
			iStep := 50;	
		ELSIF mAlgo = 3 // Manual
			THEN
			sCurrentOperation := 'Filling manually';
			iChosenSlot := iRackSlot;
			iStep := 50;
		ELSE
			// Halt operations
			iStep := 1000;
		END_IF
		
	50:
		fill.bStart := TRUE;
		fill.iSlotPosition := iChosenSlot;
		IF fill.bDone THEN
			fill.bStart := FALSE;
			iStep := 0;
		END_IF
		
		
	// ------------------------------------------------------------------------ Empty operation --------------------------------------------------------
	60: //Check algo for empty operation
	
		IF mAlgo = 1 // Horizontal
			THEN
			sCurrentOperation := 'Emptying horizontally';
			iCounter := 54;
			WHILE iCounter<>0 DO
				IF aSlots[iCounter] 
					THEN
					iChosenSlot := iCounter;
					EXIT;
				END_IF
    			iCounter := iCounter-1;
			END_WHILE
			
			iStep := 70;
		ELSIF mAlgo = 2 // Vertical
			THEN
			sCurrentOperation := 'Emptying vertically';
			FOR i := 1 TO 54 DO
				IF aSlots[aEmptyVerticalPositions[i]] 
					THEN
					iChosenSlot := aEmptyVerticalPositions[i];
					EXIT;
				END_IF
			END_FOR
			iStep := 70;
		ELSIF mAlgo = 3 // Manual
			THEN
			sCurrentOperation := 'Emptying manually';
			iChosenSlot := iRackSlot;
			iStep := 70;
		ELSE
			// Halt operations
			iStep := 1000;
		END_IF
		
	70:
		empty.bStart := TRUE;
		empty.iSlotPosition := iChosenSlot;
		IF empty.bDone THEN
			empty.bStart := FALSE;
			iStep := 80;
		END_IF
		
	80:
		fioOutputs.bExitLoadingConveyor := TRUE;
		fioOutputs.bExitRollerConveyor := TRUE;
		IF fioInputs.bExitSensor
			THEN
			iStep := 90;
		END_IF
		
	90:
		exitTimer.IN := TRUE;
		IF exitTimer.Q
			THEN
			exitTimer.IN := FALSE;
			fioOutputs.bExitLoadingConveyor := FALSE;
			fioOutputs.bExitRollerConveyor := FALSE;
			iStep:=0;
		END_IF
		
	2000:
		fioOutputs.iSCPosition := 0;
		fill.bStart := FALSE;
		empty.bStart := FALSE;
		fioOutputs.bEntryLoadingConveyor := FALSE;
		fioOutputs.bEntryRollerConveyor := FALSE;
		fioOutputs.bExitLoadingConveyor := FALSE;
		fioOutputs.bExitRollerConveyor := FALSE;
		resetInputs();	
		
	3000:
		home.bStart := TRUE;
		IF home.bDone THEN
			home.bStart := FALSE;
			iStep := 0;
		END_IF
		
END_CASE
]]></ST>
    </Implementation>
    <Method Name="resetInputs" Id="{7f98eaa7-e02f-48c5-bb0c-20e0578da105}">
      <Declaration><![CDATA[METHOD PRIVATE resetInputs : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bStart := FALSE;
bHalt := FALSE;
bResume := FALSE;
bStop := FALSE;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="StackerCraneProcess">
      <LineId Id="797" Count="3" />
      <LineId Id="802" Count="0" />
      <LineId Id="1130" Count="0" />
      <LineId Id="803" Count="0" />
      <LineId Id="1056" Count="1" />
      <LineId Id="1067" Count="0" />
      <LineId Id="1058" Count="1" />
      <LineId Id="1123" Count="0" />
      <LineId Id="1118" Count="4" />
      <LineId Id="1061" Count="0" />
      <LineId Id="1275" Count="5" />
      <LineId Id="1124" Count="0" />
      <LineId Id="1281" Count="0" />
      <LineId Id="804" Count="7" />
      <LineId Id="1249" Count="4" />
      <LineId Id="1257" Count="0" />
      <LineId Id="1256" Count="0" />
      <LineId Id="1248" Count="0" />
      <LineId Id="812" Count="17" />
      <LineId Id="1129" Count="0" />
      <LineId Id="832" Count="4" />
      <LineId Id="1000" Count="0" />
      <LineId Id="1147" Count="0" />
      <LineId Id="1001" Count="0" />
      <LineId Id="837" Count="1" />
      <LineId Id="1146" Count="0" />
      <LineId Id="1245" Count="0" />
      <LineId Id="839" Count="0" />
      <LineId Id="1046" Count="0" />
      <LineId Id="840" Count="7" />
      <LineId Id="1145" Count="0" />
      <LineId Id="1246" Count="0" />
      <LineId Id="1047" Count="1" />
      <LineId Id="1080" Count="0" />
      <LineId Id="848" Count="27" />
      <LineId Id="997" Count="0" />
      <LineId Id="876" Count="7" />
      <LineId Id="1174" Count="9" />
      <LineId Id="1173" Count="0" />
      <LineId Id="1155" Count="3" />
      <LineId Id="1154" Count="0" />
      <LineId Id="884" Count="4" />
      <LineId Id="995" Count="0" />
      <LineId Id="1062" Count="0" />
      <LineId Id="1069" Count="0" />
      <LineId Id="1063" Count="0" />
      <LineId Id="1073" Count="0" />
      <LineId Id="1064" Count="1" />
      <LineId Id="1074" Count="0" />
      <LineId Id="1078" Count="0" />
      <LineId Id="1054" Count="0" />
      <LineId Id="1076" Count="0" />
      <LineId Id="1081" Count="0" />
      <LineId Id="1083" Count="2" />
      <LineId Id="1102" Count="0" />
      <LineId Id="1098" Count="0" />
      <LineId Id="1103" Count="0" />
      <LineId Id="1106" Count="2" />
      <LineId Id="1105" Count="0" />
      <LineId Id="1100" Count="0" />
      <LineId Id="1097" Count="0" />
      <LineId Id="1092" Count="1" />
      <LineId Id="1205" Count="9" />
      <LineId Id="1184" Count="0" />
      <LineId Id="1148" Count="3" />
      <LineId Id="1153" Count="0" />
      <LineId Id="1094" Count="2" />
      <LineId Id="1082" Count="0" />
      <LineId Id="1079" Count="0" />
      <LineId Id="1109" Count="5" />
      <LineId Id="1077" Count="0" />
      <LineId Id="1116" Count="1" />
      <LineId Id="1125" Count="0" />
      <LineId Id="1127" Count="0" />
      <LineId Id="1126" Count="0" />
      <LineId Id="1131" Count="0" />
      <LineId Id="1134" Count="0" />
      <LineId Id="1132" Count="0" />
      <LineId Id="1135" Count="4" />
      <LineId Id="1143" Count="0" />
      <LineId Id="1141" Count="1" />
      <LineId Id="1144" Count="0" />
      <LineId Id="1140" Count="0" />
      <LineId Id="1159" Count="1" />
      <LineId Id="1266" Count="2" />
      <LineId Id="1271" Count="1" />
      <LineId Id="1269" Count="1" />
      <LineId Id="1247" Count="0" />
      <LineId Id="1265" Count="0" />
      <LineId Id="1258" Count="0" />
      <LineId Id="1260" Count="0" />
      <LineId Id="1262" Count="2" />
      <LineId Id="1259" Count="0" />
      <LineId Id="1274" Count="0" />
      <LineId Id="996" Count="0" />
      <LineId Id="44" Count="0" />
    </LineIds>
    <LineIds Name="StackerCraneProcess.resetInputs">
      <LineId Id="6" Count="2" />
      <LineId Id="5" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>